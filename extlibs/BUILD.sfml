load("@rules_cc//cc:defs.bzl", "cc_library")

cc_library(
    name = "stb",
    hdrs = glob([
        "extlibs/headers/stb_image/stb_image.h",
        "extlibs/headers/stb_image/stb_image_write.h",
    ]),
    strip_include_prefix = "extlibs/headers/stb_image/",
)

cc_library(
    name = "ft2",
    srcs = select({
        "@bazel_tools//platforms:windows": [
            "extlibs/libs-msvc/x64/freetype.lib",
        ],
        "@bazel_tools//platforms:linux": []
    }),
    hdrs = glob([
        "extlibs/headers/freetype2/**/*.h",
    ]),
    linkopts = select({
        "@bazel_tools//platforms:windows": [
        ],
        "@bazel_tools//platforms:linux": [
            "-lfreetype",
        ],
    }),
    strip_include_prefix = "extlibs/headers/freetype2/",
)

cc_library(
    name = "sfml-system",
    srcs = glob([
        "src/SFML/System/*.cpp",
        "src/SFML/System/*.hpp",
    ]) + select({
        "@bazel_tools//platforms:windows": glob([
            "src/SFML/System/Win32/**/*.cpp",
            "src/SFML/System/Win32/**/*.hpp",
        ]),
        "@bazel_tools//platforms:linux": glob([
            "src/SFML/System/Unix/**/*.cpp",
            "src/SFML/System/Unix/**/*.hpp",
        ]),
    }),
    hdrs = glob([
        "include/SFML/*.hpp",
        "include/SFML/System/**/*.h",
        "include/SFML/System/**/*.hpp",
        "include/SFML/System/**/*.inl",
    ]),
    copts = ["-Iexternal/sfml/src/"],
    defines = ["SFML_STATIC"],
    linkopts = select({
        "@bazel_tools//platforms:windows": [
            "-DEFAULTLIB:winmm",
        ],
        "@bazel_tools//platforms:linux": [
            "-ludev",
            "-pthread",
        ],
    }),
    strip_include_prefix = "include/",
    visibility = ["//visibility:public"],
)

cc_library(
    name = "sfml-window",
    srcs = glob(
        [
            "src/SFML/Window/*.cpp",
            "src/SFML/Window/*.hpp",
        ],
        exclude = [
            "src/SFML/Window/EglContext.cpp",
            "src/SFML/Window/EglContext.hpp",
            "src/SFML/Window/EGLCheck.cpp",
            "src/SFML/Window/EGLCheck.hpp",
        ],
    ) + select({
        "@bazel_tools//platforms:windows": glob([
            "src/SFML/Window/Win32/**/*.cpp",
            "src/SFML/Window/Win32/**/*.hpp",
        ]),
        "@bazel_tools//platforms:linux": glob([
            "src/SFML/Window/Unix/**/*.cpp",
            "src/SFML/Window/Unix/**/*.hpp",
        ]),
    }),
    hdrs = glob([
        "include/SFML/Window/**/*.h",
        "include/SFML/Window/**/*.hpp",
    ]),
    copts = ["-Iexternal/sfml/src/"],
    defines = [
        "SFML_STATIC",
        "UNICODE",
        "_UNICODE",
    ],
    linkopts = select({
        "@bazel_tools//platforms:windows": [
            "-DEFAULTLIB:advapi32",
            "-DEFAULTLIB:gdi32",
            "-DEFAULTLIB:opengl32",
            "-DEFAULTLIB:user32",
            "-DEFAULTLIB:winmm",
        ],
        "@bazel_tools//platforms:linux": [
            "-lGL",
            "-lX11",
            "-lXrandr",
            "-ludev",
        ],
    }),
    strip_include_prefix = "include/",
    visibility = ["//visibility:public"],
    deps = [
        ":sfml-system",
    ],
)

cc_library(
    name = "sfml-graphics",
    srcs = glob([
        "src/SFML/Graphics/*.cpp",
        "src/SFML/Graphics/*.hpp",
    ]) + select({
        "@bazel_tools//platforms:windows": glob([
            "src/SFML/Graphics/Win32/**/*.cpp",
            "src/SFML/Graphics/Win32/**/*.hpp",
        ]),
        "@bazel_tools//platforms:linux": glob([
            "src/SFML/Graphics/Unix/**/*.cpp",
            "src/SFML/Graphics/Unix/**/*.hpp",
        ]),
    }),
    hdrs = glob([
        "include/SFML/Graphics/**/*.h",
        "include/SFML/Graphics/**/*.inl",
        "include/SFML/Graphics/**/*.hpp",
    ]),
    copts = ["-Iexternal/sfml/src/"],
    defines = ["SFML_STATIC"],
    linkopts = select({
        "@bazel_tools//platforms:windows": [
            "-DEFAULTLIB:opengl32",
        ],
        "@bazel_tools//platforms:linux": [
            "-lGL",
            "-ludev",
            "-pthread",
        ],
    }),
    strip_include_prefix = "include/",
    visibility = ["//visibility:public"],
    deps = [
        ":ft2",
        ":sfml-window",
        ":sfml-system",
        ":stb",
    ],
)
